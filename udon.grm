


%%
(* Declarations for ML-Yacc *)

%name Udon
%pos unit
%eop SEMICOLON
%start EXP

(* Terminal symbols *)
%term IDENT of string (* value identifier *)
    | BOOL of bool
    | DIGIT of int
    | ELSE (* else *)
    | END (* end *)
    | FN (* fn *)
    | IF (* if *)
    | IN (* in *)
    | INFIXL (* infixl *)
    | INFIXR (* infixr *)
    | LET (* let *)
    | NONFIX (* nonfix *)
    | OP (* op *)
    | REC (* rec *)
    | THEN (* then *)
    | VAL (* val *)
    | EQUAL (* = *)
    | COMMA (* , *)
    | LPAREN (* ( *)
    | RPAREN (* ) *)
    | FATARROW (* => *)
    | SEMICOLON (* ; *)

(* Nonterminal symbols *)
%nonterm VID of string
       | SCON of Const.t
       | ATEXP of Syntax.exp
       | ATEXPSEQ_AUX of Syntax.exp list
       | ATEXPSEQ of Syntax.exp list
       | VIDSEQ_AUX of string list
       | VIDSEQ of string list
       | APPEXP of Syntax.exp
       | APPEXPSEQ_AUX of Syntax.exp list
       | APPEXPSEQ of Syntax.exp list
       | EXP of Syntax.exp
       | ATDEC of Syntax.dec
       | DEC of Syntax.dec list

%%

(* Production rules *)
VID : IDENT
          (IDENT)
    | EQUAL
          ("=")

SCON : BOOL
          (Const.BOOL BOOL)
     | DIGIT
          (Const.INT DIGIT)

ATEXP : SCON
          (Syntax.CONST SCON)
      | VID
          (Syntax.VAR VID)
      | OP VID
          (Syntax.VAR VID)
      | LET DEC IN EXP END
          (Syntax.LET (DEC, EXP))
      | LPAREN EXP RPAREN
          (EXP)

ATEXPSEQ_AUX : ATEXP
                 ([ATEXP])
             | ATEXP COMMA ATEXPSEQ_AUX
                 (ATEXP :: ATEXPSEQ_AUX)

ATEXPSEQ : LPAREN RPAREN
             ([])
         | ATEXP
             ([ATEXP])
         | LPAREN ATEXPSEQ_AUX RPAREN
             (ATEXPSEQ_AUX)

VIDSEQ_AUX : VID
               ([VID])
           | VID COMMA VIDSEQ_AUX
               (VID :: VIDSEQ_AUX)

VIDSEQ : LPAREN RPAREN
           ([])
       | VID
           ([VID])
       | LPAREN VIDSEQ_AUX RPAREN
           (VIDSEQ_AUX)

APPEXPSEQ_AUX : APPEXP
		  ([APPEXP])
	      | APPEXP COMMA APPEXPSEQ_AUX
		  (APPEXP :: APPEXPSEQ_AUX)

APPEXPSEQ : LPAREN RPAREN
	      ([])
	  | LPAREN APPEXPSEQ_AUX RPAREN
	      (APPEXPSEQ_AUX)

APPEXP : ATEXP
           (ATEXP)
       | APPEXP ATEXPSEQ
           (Syntax.APP (APPEXP, ATEXPSEQ))

EXP : APPEXP
        (APPEXP)
    | FN VIDSEQ FATARROW EXP
        (Syntax.ABS (VIDSEQ, EXP))
    | IF EXP THEN EXP ELSE EXP
        (Syntax.IF (EXP1, EXP2, EXP3))

ATDEC : VAL VID EQUAL EXP
	  (Syntax.VAL (VID, EXP))
      | VAL REC VID EQUAL FN VIDSEQ FATARROW EXP
	  (Syntax.VALREC (VID, VIDSEQ, EXP))

DEC : ATDEC
        ([ATDEC])
    | ATDEC SEMICOLON DEC
        (ATDEC :: DEC)
