


%%
(* Declarations for ML-Yacc *)

%name Udon
%pos unit
%eop SEMICOLON
%start EXP

(* Terminal symbols *)
%term VID of string (* value identifier *)
    | SCON of Const.t (* special constant *)
    | ELSE (* else *)
    | END (* end *)
    | FN (* fn *)
    | IF (* if *)
    | IN (* in *)
    | LET (* let *)
    | OP (* op *)
    | REC (* rec *)
    | THEN (* then *)
    | VAL (* val *)
    | EQUAL (* = *)
    | COMMA (* , *)
    | PLUS (* + *)
    | MINUS (* - *)
    | AST (* * *)
    | LPAREN (* ( *)
    | RPAREN (* ) *)
    | LE (* <= *)
    | FATARROW (* => *)
    | SEMICOLON (* ; *)

(* Nonterminal symbols *)
%nonterm ATEXP of Syntax.exp
       | ATEXPSEQ_AUX of Syntax.exp list
       | ATEXPSEQ of Syntax.exp list
       | VIDSEQ_AUX of Id.t list
       | VIDSEQ of Id.t list
       | PRIM of Prim.t
       | APPEXP of Syntax.exp
       | APPEXPSEQ_AUX of Syntax.exp list
       | APPEXPSEQ of Syntax.exp list
       | EXP of Syntax.exp

%%

(* Production rules *)
ATEXP : SCON
          (Syntax.CONST SCON)
      | VID
          (Syntax.VAR VID)
      | LET VAL VID EQUAL EXP IN EXP END
          (Syntax.LET_VAL (VID, EXP1, EXP2))
      | LET VAL REC VID EQUAL FN VIDSEQ FATARROW EXP IN EXP END
          (Syntax.LET_VALREC (VID, VIDSEQ, EXP1, EXP2))
      | LPAREN EXP RPAREN
          (EXP)

ATEXPSEQ_AUX : ATEXP
                 ([ATEXP])
             | ATEXP COMMA ATEXPSEQ_AUX
                 (ATEXP :: ATEXPSEQ_AUX)

ATEXPSEQ : LPAREN RPAREN
             ([])
         | ATEXP
             ([ATEXP])
         | LPAREN ATEXPSEQ_AUX RPAREN
             (ATEXPSEQ_AUX)

VIDSEQ_AUX : VID
               ([VID])
           | VID COMMA VIDSEQ
               (VID :: VIDSEQ)

VIDSEQ : LPAREN RPAREN
           ([])
       | VID
           ([VID])
       | LPAREN VIDSEQ_AUX RPAREN
           (VIDSEQ_AUX)

PRIM : PLUS
         (Prim.PLUS)
     | MINUS
         (Prim.MINUS)
     | AST
         (Prim.TIMES)
     | LE
         (Prim.LE)

APPEXPSEQ_AUX : APPEXP
		  ([APPEXP])
	      | APPEXP COMMA APPEXPSEQ_AUX
		  (APPEXP :: APPEXPSEQ_AUX)

APPEXPSEQ : LPAREN RPAREN
	      ([])
	  | LPAREN APPEXPSEQ_AUX RPAREN
	      (APPEXPSEQ_AUX)

APPEXP : ATEXP
           (ATEXP)
       | APPEXP ATEXPSEQ
           (Syntax.APP (APPEXP, ATEXPSEQ))
       | OP PRIM APPEXPSEQ
           (Syntax.PRIM (PRIM, APPEXPSEQ))

EXP : APPEXP
        (APPEXP)
    | FN VIDSEQ FATARROW EXP
        (Syntax.ABS (VIDSEQ, EXP))
    | IF EXP THEN EXP ELSE EXP
        (Syntax.IF (EXP1, EXP2, EXP3))
