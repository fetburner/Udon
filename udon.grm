


%%
(* Declarations for ML-Yacc *)

%name Udon
%pos unit
%eop SEMICOLON
%start EXP

(* Terminal symbols *)
%term IDENT of string (* value identifier *)
    | BOOL of bool
    | DIGIT of int
    | CASE (* case *)
    | ELSE (* else *)
    | END (* end *)
    | FN (* fn *)
    | IF (* if *)
    | IN (* in *)
    | INFIX (* infix *)
    | INFIXR (* infixr *)
    | LET (* let *)
    | NONFIX (* nonfix *)
    | OF (* of *)
    | OP (* op *)
    | REC (* rec *)
    | THEN (* then *)
    | VAL (* val *)
    | EQUAL (* = *)
    | COMMA (* , *)
    | LPAREN (* ( *)
    | RPAREN (* ) *)
    | FATARROW (* => *)
    | SEMICOLON (* ; *)

(* Nonterminal symbols *)
%nonterm VID of string
       | SCON of Const.t
       | ATEXP of ConcreteSyntax.exp
       | VIDSEQ_AUX of string list
       | VIDSEQ of string list
       | SEQEXP of ConcreteSyntax.exp list
       | EXP of ConcreteSyntax.exp
       | ATDEC of ConcreteSyntax.dec
       | DEC of ConcreteSyntax.dec list
       | TUPLE_BODY of ConcreteSyntax.exp list
       | DIGIT_OPT of int option
       | NVID of string list

%%

(* Production rules *)
VID : IDENT
          (IDENT)
    | EQUAL
          ("=")

SCON : BOOL
          (Const.BOOL BOOL)
     | DIGIT
          (Const.INT DIGIT)

ATEXP : SCON
          (ConcreteSyntax.CONST SCON)
      | LPAREN RPAREN
          (ConcreteSyntax.TUPLE [])
      | LPAREN TUPLE_BODY RPAREN
          (ConcreteSyntax.TUPLE TUPLE_BODY)
      | VID
          (ConcreteSyntax.VAR VID)
      | OP VID
          (ConcreteSyntax.OP VID)
      | LET DEC IN EXP END
          (ConcreteSyntax.LET (DEC, EXP))
      | LPAREN EXP RPAREN
          (ConcreteSyntax.PAREN EXP)

VIDSEQ_AUX : VID
               ([VID])
           | VID COMMA VIDSEQ_AUX
               (VID :: VIDSEQ_AUX)

VIDSEQ : LPAREN RPAREN
           ([])
       | VID
           ([VID])
       | LPAREN VIDSEQ_AUX RPAREN
           (VIDSEQ_AUX)

SEQEXP : ATEXP
           ([ATEXP])
       | ATEXP SEQEXP
           (ATEXP :: SEQEXP)

EXP : SEQEXP
        (ConcreteSyntax.SEQ SEQEXP)
    | FN VID FATARROW EXP
        (ConcreteSyntax.ABS (VID, EXP))
    | IF EXP THEN EXP ELSE EXP
        (ConcreteSyntax.IF (EXP1, EXP2, EXP3))
    | CASE EXP OF VIDSEQ FATARROW EXP
        (ConcreteSyntax.CASE (EXP1, VIDSEQ, EXP2))

ATDEC : VAL VID EQUAL EXP
          (ConcreteSyntax.VAL (VID, EXP))
      | VAL REC VID EQUAL EXP
          (ConcreteSyntax.VALREC (VID, EXP))
      | INFIX DIGIT_OPT NVID
          (ConcreteSyntax.INFIX (Assoc.LEFT_ASSOC, getOpt (DIGIT_OPT, 0), NVID))
      | INFIXR DIGIT_OPT NVID
          (ConcreteSyntax.INFIX (Assoc.RIGHT_ASSOC, getOpt (DIGIT_OPT, 0), NVID))
      | NONFIX NVID
          (ConcreteSyntax.NONFIX NVID)

DEC : ATDEC
        ([ATDEC])
    | ATDEC SEMICOLON DEC
        (ATDEC :: DEC)
    | ATDEC DEC
        (ATDEC :: DEC)

TUPLE_BODY : EXP COMMA EXP
               ([EXP1, EXP2])
           | EXP COMMA TUPLE_BODY
               (EXP :: TUPLE_BODY)

DIGIT_OPT :
              (NONE)
          | DIGIT
              (SOME DIGIT)

NVID : VID
         ([VID])
     | VID NVID
         (VID :: NVID)
